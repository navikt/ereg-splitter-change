plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.6.21' //1.6.21
    id 'com.google.protobuf' version '0.8.11'
    id 'org.jmailen.kotlinter' version '3.0.0'
    id 'com.github.johnrengelman.shadow' version '8.0.0'
}

group 'no.nav.ereg'
version '0.7'

jar {
    manifest {
        attributes 'Main-Class': 'no.nav.ereg.MainKt'
    }
}

shadowJar {
    archiveBaseName = 'app'
    archiveClassifier = ''
}

application {
    mainClassName = 'no.nav.ereg.MainKt'
}

tasks.withType(JavaExec) {
    jvmArgs = ['-Xms4096m', '-Xmx8192m']
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://packages.confluent.io/maven"
    }

    ["maven-release"].each { repo ->
        String token = System.getenv("GITHUB_TOKEN") ?: project.findProperty("githubPassword")
        if (!token) throw new NullPointerException("Missing token, you have to set GITHUB_TOKEN or githubPassword")
        maven {
            url = uri("https://maven.pkg.github.com/navikt/$repo")
            credentials {
                username = 'x-access-token'
                password = token
            }
        }
    }
}

configurations {
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}


dependencies {
    implementation 'com.google.code.gson:gson:2.10'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5"

    implementation "org.http4k:http4k-server-apache:3.251.0"
    implementation "org.http4k:http4k-client-apache:3.251.0"

    implementation "org.apache.kafka:kafka-clients:7.5.1-ce"

    implementation "io.grpc:grpc-protobuf:1.60.0"
    implementation "io.grpc:grpc-stub:1.60.0"

    implementation "io.github.microutils:kotlin-logging:1.7.9"
    implementation "ch.qos.logback:logback-classic:1.4.14"
    implementation "net.logstash.logback:logstash-logback-encoder:7.4"

    implementation "io.prometheus:simpleclient_common:0.8.1"
    implementation "io.prometheus:simpleclient_hotspot:0.8.1"

    implementation "org.apache.commons:commons-compress:1.24.0" //transient http4k-apache
    implementation "org.apache.httpcomponents:httpclient:4.5.13" //Desired transient httpclient to http4k-apache
    implementation "org.xerial.snappy:snappy-java:1.1.10.4" //Desired transient snappy to kafka-clients above
    implementation "org.apache.avro:avro:1.11.3" //Desired transient avro to serializer above
}

test {
    useJUnitPlatform()
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.kotlin.srcDirs += 'src/test/kotlin'
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.11.0' }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.60.0" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

processResources.dependsOn {
    extractProto
}

processTestResources.dependsOn {
    extractTestProto
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}